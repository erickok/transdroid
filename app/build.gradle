apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 31
        versionCode 241
        versionName '2.5.21'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "resourcePackageName": "org.transdroid",
                        "androidManifestFile": file("src/main/AndroidManifest.xml").absolutePath
                ]
            }
        }
    }

    signingConfigs {
        releaseConfig {
            def propsFile = rootProject.file('keystore.properties')
            def configName = 'releaseConfig'

            if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                keyAlias = props['keyAlias']
                storeFile = file(props['storeFile'])
                keyPassword = props['keyPassword']
                storePassword = props['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        full {
            dimension "version"
            applicationId 'org.transdroid.full'
            resValue "string", "search_history_authority", applicationId + ".search.SearchHistoryProvider"
        }
        lite {
            dimension "version"
            applicationId 'org.transdroid.lite'
            resValue "string", "search_history_authority", applicationId + ".search.SearchHistoryProvider"
        }
    }

    lintOptions {
        disable 'MissingTranslation', 'ExtraTranslation', 'StringFormatInvalid', 'ValidFragment', 'Registered'
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    // Android support
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'

    // Other
    implementation 'org.androidannotations:androidannotations-api:4.7.0'
    implementation 'org.androidannotations:ormlite-api:4.7.0'
    implementation 'com.j256.ormlite:ormlite-core:5.1'
    implementation 'com.j256.ormlite:ormlite-android:5.1'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.nispok:snackbar:2.11.0'
    implementation 'org.apache.openjpa:openjpa-lib:3.1.1'
    implementation 'net.iharder:base64:2.3.9'
    implementation('com.github.afollestad.material-dialogs:core:0.9.6.0@aar') {
        transitive = true
    }
    implementation 'com.evernote:android-job:1.2.6'

    annotationProcessor 'org.androidannotations:androidannotations:4.7.0'
    annotationProcessor 'org.androidannotations:ormlite:4.7.0'
}
